import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile


plugins {
  id "org.jetbrains.kotlin.jvm"
  id "java-gradle-plugin"
}

gradlePlugin {
  plugins {
    laboratory {
      id = "io.mehow.laboratory"
      implementationClass = "io.mehow.laboratory.gradle.LaboratoryPlugin"
    }
  }
}

test.useJUnitPlatform()

configurations {
  fixtureClasspath
}

tasks.getByName("pluginUnderTestMetadata").getPluginClasspath().from(configurations.fixtureClasspath)

dependencies {
  implementation project(":generator")
  implementation libs.kotlin.gradlePlugin
  compileOnly libs.android.gradlePlugin

  testImplementation libs.kotest.runnerJunit5
  testImplementation libs.kotest.assertions

  fixtureClasspath libs.kotlin.gradlePlugin
  fixtureClasspath libs.android.gradlePlugin
}

def versionDir = "${buildDir}/generated/source/laboratory"
sourceSets.main.java.srcDirs += versionDir

def generateVersion = tasks.register("pluginVersion") {
  inputs.property "version", version
  outputs.dir file(versionDir)

  doLast {
    def outputFile = file("$versionDir/io/mehow/laboratory/Config.kt")
    outputFile.parentFile.mkdirs()
    outputFile.text = """package io.mehow.laboratory

internal const val laboratoryVersion = "$version"
"""
  }
}

tasks.withType(KotlinCompile).configureEach {
  it.dependsOn(generateVersion)
}

apply from: "$rootDir/gradle/dokka-config.gradle"
apply from: "$rootDir/gradle/gradle-mvn-push.gradle"

tasks.withType(Jar).configureEach {
  it.dependsOn(generateVersion)
}

tasks.withType(DokkaTask).configureEach {
  it.dependsOn(generateVersion)
}
