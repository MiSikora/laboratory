plugins {
  id "com.android.application"
  id "org.jetbrains.kotlin.android"
  id "io.mehow.laboratory"
}

android {
  namespace "io.mehow.laboratory.sample.basic"

  signingConfigs {
    config {
      keyAlias "mehow-io"
      keyPassword "mehow-io"
      storeFile file("../mehow-io.keystore")
      storePassword "mehow-io"
    }
  }

  defaultConfig {
    applicationId "io.mehow.laboratory.sample.basic"
    targetSdkVersion versions.androidBuild.targetSdk

    versionCode 1
    versionName "1.0.0"

    signingConfig signingConfigs.config
  }

  buildFeatures {
    viewBinding true
  }

  buildTypes.debug.setMatchingFallbacks "release"

  variantFilter {
    setIgnore it.name != "debug"
  }

  compileOptions {
    sourceCompatibility JavaConfig.code
    targetCompatibility JavaConfig.code
  }

  kotlinOptions {
    jvmTarget = JavaConfig.name
    freeCompilerArgs += "-Xjvm-default=all"
  }
}

laboratory {
  packageName = "io.mehow.laboratory.sample.basic"

  featureFactory()

  feature("LogType") {
    deprecated("Sample deprecation")

    withDefaultOption("Info")
    withOption("Verbose")
    withOption("Debug")
    withOption("Warning")
    withOption("Error")
  }

  feature("ReportRootedDevice") {
    description =
        "Reports during [cold start](https://developer.android.com/topic/performance/vitals/launch-time#cold) whether device is rooted"

    withDefaultOption("Disabled")
    withOption("Enabled")
  }

  feature("Authentication") {
    withDefaultOption("Password")
    withOption("Fingerprint")
    withOption("Retina")
    withOption("Face")
  }
}

dependencies {
  implementation libs.kotlin.x.coroutinesAndroid
  implementation libs.android.material
  implementation libs.hyperion.core
  implementation libs.laboratory.dataStore
  implementation libs.laboratory.hyperionPlugin
}
