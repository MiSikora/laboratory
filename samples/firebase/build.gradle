plugins {
  id "com.android.application"
  id "org.jetbrains.kotlin.android"
  id "io.mehow.laboratory"
  id "com.google.gms.google-services"
}

android {
  namespace "io.mehow.laboratory.sample.firebase"

  compileSdkVersion versions.androidBuild.compileSdk

  signingConfigs {
    config {
      keyAlias "mehow-io"
      keyPassword "mehow-io"
      storeFile file("../mehow-io.keystore")
      storePassword "mehow-io"
    }
  }

  defaultConfig {
    applicationId "io.mehow.laboratory.sample.firebase"

    minSdkVersion versions.androidBuild.minSdk
    targetSdkVersion versions.androidBuild.targetSdk

    versionCode 1
    versionName "1.0.0"

    signingConfig signingConfigs.config
  }

  buildFeatures {
    viewBinding true
  }

  buildTypes.debug.setMatchingFallbacks "release"

  variantFilter {
    setIgnore it.name != "debug"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_17.toString()
    freeCompilerArgs += "-Xjvm-default=all"
  }
}

laboratory {
  packageName = "io.mehow.laboratory.sample.firebase"

  sourcedStorage()
  optionFactory()
  featureFactory()

  feature("LogType") {
    key = "LogType"

    withDefaultOption("Info")
    withOption("Verbose")
    withOption("Debug")
    withOption("Warning")
    withOption("Error")

    withDefaultSource("Firebase")
  }

  feature("ReportRootedDevice") {
    key = "ReportRootedDevice"

    withDefaultOption("Disabled")
    withOption("Enabled")

    withDefaultSource("Firebase")
  }

  feature("Authentication") {
    key = "Authentication"

    withDefaultOption("Password")
    withOption("Fingerprint")
    withOption("Retina")
    withOption("Face")

    withDefaultSource("Firebase")
  }
}

dependencies {
  implementation libs.kotlin.x.coroutinesAndroid
  implementation libs.android.material
  implementation libs.firebase.databaseKtx
  implementation libs.hyperion.core
  implementation libs.laboratory.dataStore
  implementation libs.laboratory.hyperionPlugin
}
