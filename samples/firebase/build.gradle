plugins {
  id "com.android.application"
  id "org.jetbrains.kotlin.android"
  id "io.mehow.laboratory"
  id "com.google.gms.google-services"
}

android {
  compileSdkVersion versions.androidBuild.compileSdk

  signingConfigs {
    config {
      keyAlias "mehow-io"
      keyPassword "mehow-io"
      storeFile file("../mehow-io.keystore")
      storePassword "mehow-io"
    }
  }

  defaultConfig {
    applicationId "io.mehow.laboratory.sample.firebase"

    minSdkVersion versions.androidBuild.minSdk
    targetSdkVersion versions.androidBuild.targetSdk

    versionCode 1
    versionName "1.0.0"

    signingConfig signingConfigs.config
  }

  buildFeatures {
    viewBinding true
  }

  buildTypes.debug.setMatchingFallbacks "release"

  variantFilter {
    setIgnore it.name != "debug"
  }

  compileOptions {
    kotlinOptions.freeCompilerArgs += [
        "-Xjvm-default=all",
        "-Xopt-in=kotlin.RequiresOptIn",
    ]
  }
}

laboratory {
  packageName = "io.mehow.laboratory.sample.firebase"

  // Filters are needed because otherwise feature flags from other samples would be included.
  sourcedStorage {
    projectFilter { it == project }
  }
  optionFactory {
    projectFilter { it == project }
  }
  featureFactory {
    projectFilter { it == project }
  }

  LogType {
    key = "LogType"

    withDefaultOption("Info")
    withOption("Verbose")
    withOption("Debug")
    withOption("Warning")
    withOption("Error")

    withDefaultSource("Firebase")
  }

  ReportRootedDevice {
    key = "ReportRootedDevice"

    withDefaultOption("Disabled")
    withOption("Enabled")

    withDefaultSource("Firebase")
  }

  Authentication {
    key = "Authentication"

    withDefaultOption("Password")
    withOption("Fingerprint")
    withOption("Retina")
    withOption("Face")

    withDefaultSource("Firebase")
  }
}

dependencies {
  implementation libs.kotlin.x.coroutinesAndroid
  implementation libs.android.material
  implementation libs.firebase.databaseKtx
  implementation libs.hyperion.core
  implementation libs.laboratory.dataStore
  implementation libs.laboratory.hyperionPlugin
}
